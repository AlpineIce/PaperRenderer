#version 460

#extension GL_EXT_scalar_block_layout : require
#extension GL_EXT_buffer_reference : require
#extension GL_EXT_shader_explicit_arithmetic_types_int64 : require //for pointer arithmetic

layout (local_size_x = 128) in;

//----------GLOBAL INPUT DATA----------//

layout(std430, buffer_reference, buffer_reference_align = 8) PreprocessBufferReference;

struct CullingData
{
    vec4 frustum; //(left, right, top, bottom)
    vec2 zPlanes; //(near, far)
    vec2 padding;
};

layout(std430, set = 0, binding = 0) uniform InputData
{
    PreprocessBufferReference bufferReference;
    uint64_t padding;
    vec4 camPos;
    mat4 projection;
    mat4 view;
    CullingData cullingData;
    uint objectCount;
    
} inputData;

struct OBB
{
    float posX;
    float negX;
    float posY;
    float negY;
    float posZ;
    float negZ;
};

struct InputObject
{
    vec4 position; //w as padding
    vec4 scale;
    mat4 rotation;
    OBB bounds;
    uint lodCount;
    uint lodOffset;
};

layout(std430, set = 0, binding = 1) readonly buffer InputObjects
{
    InputObject objects[];
} inputObjects;

struct MeshReference
{
    uint meshOffset;
};

layout(std430, buffer_reference, buffer_reference_align = 8) readonly buffer MeshReferences
{
    MeshReference references[];
};

struct LOD
{
    uint meshesReferencesOffset;
    uint meshReferenceCount;
};

layout(std430, buffer_reference, buffer_reference_align = 8) readonly buffer LODs
{
    LOD lods[];
};

struct Mesh
{
    uint vboOffset;
    uint vertexCount;
    uint iboOffset;
    uint indexCount;

    uint drawCountsOffset;
    uint drawCommandsOffset;
    uint outputObjectsOffset;
    uint padding;
};

layout(std430, buffer_reference, buffer_reference_align = 8) readonly buffer MeshPointer
{
    Mesh mesh;
};

layout(std430, buffer_reference, buffer_reference_align = 8) buffer DrawCounts
{
    uint count;
};

struct DrawCommand //20 bytes
{
    uint indexCount;
    uint instanceCount;
    uint firstIndex;
    int vertexOffset;
    uint firstInstance;
};

layout(std430, buffer_reference, buffer_reference_align = 8) writeonly buffer DrawCommands
{
    DrawCommand commands[];
};

struct OutputObject
{
    mat4 model;
    mat4 objectTransform;
};

layout(std430, buffer_reference, buffer_reference_align = 8) writeonly buffer OutputObjects
{
    OutputObject objects[];
};

//----------FUNCTIONS----------//

bool isInBounds(InputObject object, mat4 modelMatrix)
{
    OBB bounds = object.bounds;
                
    //construct 8 vertices
    vec3 vertices[8];
    vertices[0] = vec3(bounds.posX, bounds.posY, bounds.posZ);      // + + +
    vertices[1] = vec3(bounds.posX, bounds.posY, bounds.negZ);      // + + -
    vertices[2] = vec3(bounds.negX, bounds.posY, bounds.posZ);      // - + +
    vertices[3] = vec3(bounds.posX, bounds.negY, bounds.posZ);      // + - +
    vertices[4] = vec3(bounds.posX, bounds.negY, bounds.negZ);      // + - -
    vertices[5] = vec3(bounds.negX, bounds.posY, bounds.negZ);      // - + -
    vertices[6] = vec3(bounds.negX, bounds.negY, bounds.posZ);      // - - +
    vertices[7] = vec3(bounds.negX, bounds.negY, bounds.negZ);      // - - -

    //transform vertices and construct AABB
    OBB AABB;
    AABB.negX = 1000000.0f;
    AABB.negY = 1000000.0f;
    AABB.negZ = 1000000.0f;
    AABB.posX = -1000000.0f;
    AABB.posY = -1000000.0f;
    AABB.posZ = -1000000.0f;
    for(int i = 0; i < 8; i++)
    {
        vertices[i] = (inputData.view * modelMatrix * vec4(vertices[i], 1.0)).xyz;
        AABB.posX = max(vertices[i].x, AABB.posX);
        AABB.negX = min(vertices[i].x, AABB.negX);
        AABB.posY = max(vertices[i].y, AABB.posY);
        AABB.negY = min(vertices[i].y, AABB.negY);
        AABB.posZ = max(vertices[i].z, AABB.posZ);
        AABB.negZ = min(vertices[i].z, AABB.negZ);
    }
    
    //visibility test
    bool visible = true;
    visible = visible && AABB.negZ < 0.0; //z test (cut everything behind)
    visible = visible && !((AABB.posX < ((inputData.cullingData.frustum.y / inputData.cullingData.frustum.x) * -AABB.negZ)) || //check left
                           (AABB.negX > ((inputData.cullingData.frustum.y / inputData.cullingData.frustum.x) * AABB.negZ))); //check right
    visible = visible && !((AABB.posY < (inputData.cullingData.frustum.z * AABB.negZ)) || //check top
                           (AABB.negY > (inputData.cullingData.frustum.z * -AABB.negZ))); //check bottom
    
	return visible;
}

uint getLODLevel(InputObject object)
{
    //get largest OBB extent to be used as size
    OBB bounds = object.bounds;
    float xLength = bounds.posX - bounds.negX;
    float yLength = bounds.posY - bounds.negY;
    float zLength = bounds.posZ - bounds.negZ;

    float worldSize = 0.0;
    worldSize = max(worldSize, xLength);
    worldSize = max(worldSize, yLength);
    worldSize = max(worldSize, zLength);

    float cameraDistance = length(object.position.xyz - inputData.camPos.xyz);
    
    uint lodLevel = uint(floor(inversesqrt(worldSize * 10.0) * sqrt(cameraDistance)));

    return lodLevel;
}

DrawCommand buildDrawCommand(Mesh mesh, uint writeIndex)
{
    DrawCommand drawCommand;
    drawCommand.indexCount = mesh.indexCount;
    drawCommand.instanceCount = 1;
    drawCommand.firstIndex = mesh.iboOffset;
    drawCommand.vertexOffset = int(mesh.vboOffset);
    drawCommand.firstInstance = writeIndex;

    return drawCommand;
}

OutputObject buildOutputObject(mat4 modelMatrix)
{
    OutputObject outputObject;
    outputObject.model = modelMatrix;
    outputObject.objectTransform = inputData.projection * inputData.view * modelMatrix;

    return outputObject;
}

void main()
{
    uint gID = gl_GlobalInvocationID.x;
    if(gID >= inputData.objectCount)
    {
        return;
    }
    InputObject inputObject = inputObjects.objects[gID];

    //transformation matrix
    mat4 modelMatrix = mat4(
        vec4(inputObject.scale.x, 0.0, 0.0, 0.0),
        vec4(0.0, inputObject.scale.y, 0.0, 0.0),
        vec4(0.0, 0.0, inputObject.scale.z, 0.0),
        vec4(inputObject.position.xyz, 1.0));
    modelMatrix = modelMatrix * inputObject.rotation;

    if(true)
    {   
        //get LOD and mesh references
        uint lodLevel = min(getLODLevel(inputObject), inputObject.lodCount - 1);
        LOD lod = LODs(uint64_t(inputData.bufferReference) + inputObject.lodOffset).lods[lodLevel];
        MeshReferences meshReferences = MeshReferences(uint64_t(inputData.bufferReference) + lod.meshesReferencesOffset);

        //loop through LOD meshes and create draw calls with counts
        for(uint i = 0; i < lod.meshReferenceCount; i++)
        {
            Mesh mesh = MeshPointer(uint64_t(inputData.bufferReference) + meshReferences.references[i].meshOffset).mesh;

            //draw counts and output objects
            DrawCounts drawCounts = DrawCounts(uint64_t(inputData.bufferReference) + mesh.drawCountsOffset);
            DrawCommands outputCommands = DrawCommands(uint64_t(inputData.bufferReference) + mesh.drawCommandsOffset);
            OutputObjects outputObjects = OutputObjects(uint64_t(inputData.bufferReference) + mesh.outputObjectsOffset);

            uint writeIndex = atomicAdd(drawCounts.count, 1);

            //draw commands
            outputCommands.commands[writeIndex] = buildDrawCommand(mesh, writeIndex);

            //output objects
            outputObjects.objects[writeIndex] = buildOutputObject(modelMatrix);
            
        }
    }
}